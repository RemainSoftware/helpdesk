 /* ---------------------------------------------------------------- */
 /*  Copyright (c) 2019 Remain BV (https://remainsoftware.com)       */
 /*                                                                  */
 /*  This program and the accompanying materials are made            */
 /*  available under the terms of the Eclipse Public License 2.0     */
 /*  which accompanies this distribution, and is available at        */
 /*  https://www.eclipse.org/legal/epl-2.0/                          */
 /*                                                                  */
 /*  SPDX-License-Identifier: EPL-2.0                                */
 /*                                                                  */
 /*  Contributors:                                                   */
 /*      Remain Software - initial API and implementation            */
 /*                                                                  */
 /* ---------------------------------------------------------------- */


 /*   *$$\CRTOM CRTCLPGM    */
 /*   *$$ ALWRTVSRC(*NO)    */
 /*   *$$ TGTRLS(V5R2M0)    */
 /*   *$$ REPLACE(*YES)     */
 /*   *$$\ENDOM             */
 /* ---------------------------------------------------------------- */
 /* Description                                                      */
 /* ---------------------------------------------------------------- */
 /*                                                                  */
 /* Program ....: CRTDDMHDC                                          */
 /* Function ...: Create DDM HelpDesk library                        */
 /* Author .....: Remain B.V.                                        */
 /* Startdate ..: ............                                       */
 /* Parameters .: &HDOL, HelpDesk object library      (INPUT)        */
 /*               &HDSL, HelpDesk save library        (INPUT)        */
 /*               &RMTL, Remote location.             (INPUT)        */
 /*                                                                  */
 /* This procedure is the CRTDDMHD command processor.                */
 /*                                                                  */
 /* ---------------------------------------------------------------- */

 /* ---------------------------------------------------------------- */
 /* Initialization                                                   */
 /* ---------------------------------------------------------------- */

          /* ------------------------------------------------------- */
          /* Program start                                           */
          /* ------------------------------------------------------- */
             PGM        PARM(&HDOL &HDSL &RMTL)

          /* ------------------------------------------------------- */
          /* Declare parameters                                      */
          /* ------------------------------------------------------- */
             DCL        VAR(&HDOL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&HDSL) TYPE(*CHAR) LEN(10)
             DCL        VAR(&RMTL) TYPE(*CHAR) LEN(8)

          /* ------------------------------------------------------- */
          /* Outfile declaration                                     */
          /* ------------------------------------------------------- */
             DCLF       FILE(CRTDDMHD)

 /* ---------------------------------------------------------------- */
 /* Test if the HelpDesk library exists                              */
 /* ---------------------------------------------------------------- */
             CHKOBJ     OBJ(&HDOL/OMHDD) OBJTYPE(*FILE)

 /* ---------------------------------------------------------------- */
 /* Test if the SaveLibrary exists                                   */
 /* ---------------------------------------------------------------- */
             CHKOBJ     OBJ(QSYS/&HDSL) OBJTYPE(*LIB)

 /* ---------------------------------------------------------------- */
 /* Create outfile with files.                                       */
 /* ---------------------------------------------------------------- */
             DSPFD      FILE(&HDOL/*ALL) TYPE(*BASATR) +
                          OUTPUT(*OUTFILE) FILEATR(*PF *LF) +
                          OUTFILE(QTEMP/CRTDDMHD)

 /* ---------------------------------------------------------------- */
 /* Override                                                         */
 /* ---------------------------------------------------------------- */
             OVRDBF     FILE(CRTDDMHD) TOFILE(QTEMP/CRTDDMHD) +
                          SHARE(*YES)

 /* ---------------------------------------------------------------- */
 /* Read loop                                                        */
 /* ---------------------------------------------------------------- */
 LOOP:
             RCVF
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ENDPGM))

             MOVOBJ     OBJ(&ATLIB/&ATFILE) OBJTYPE(*FILE) TOLIB(&HDSL)

             CRTDDMF    FILE(&HDOL/&ATFILE) RMTFILE(&HDOL/&ATFILE) +
                          RMTLOCNAME(&RMTL) TEXT(&ATTXT)

             GOTO       CMDLBL(LOOP)

 /* ---------------------------------------------------------------- */
 /* Exit program processing                                          */
 /* ---------------------------------------------------------------- */
 ENDPGM:
             RETURN
             ENDPGM
